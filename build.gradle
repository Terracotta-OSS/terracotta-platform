import org.terracotta.build.conventions.BaseConvention

plugins {
  id 'org.owasp.dependencycheck'
  id 'org.terracotta.build.convention.base'
}

gradle.includedBuilds.forEach { includedBuild ->
  project.tasks.named('check') { parentBuildCheckTask ->
    parentBuildCheckTask.dependsOn(includedBuild.task(':check'))
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

assert JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_8) : 'Terracotta Platform requires Java 8 or later to build'

// Attention: these defaults may change in CI, see TcCi plugin
dependencyCheck {
  cveValidForHours=24
  failBuildOnCVSS = 0 // Fail on all when not in CI (0-10)
  suppressionFile = 'config/owasp-suppressions.xml'
  skipConfigurations += ['checkstyle', 'spotbugs', 'asciidoctor']
  analyzers {
    assemblyEnabled = false // no .NET here, prevent warnings
  }
}

allprojects {
  afterEvaluate { Project p ->
    if (p.getPlugins().hasPlugin(BasePlugin)) {
      assert p.getPlugins().hasPlugin(BaseConvention)
    }
  }
}
